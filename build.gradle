plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.8.1'
    id "com.jfrog.bintray" version "1.8.0"
    id "com.diffplug.gradle.spotless" version "3.7.0"
}

scmVersion {
    tag {
        prefix = ''
    }
    hooks {
        pre 'fileUpdate', [
          file: 'README.md', pattern: {v, c -> /<version>$v/}, replacement: {v, c -> "<version>$v"}
        ]
        pre 'fileUpdate', [
          file: 'README.md', pattern: {v, c -> /com.wescale:mjml-processor:$v/}, replacement: {v, c -> "com.wescale:mjml-processor:$v"}
        ]
        pre 'commit'
    }
}

project.version = scmVersion.version
group 'com.wescale'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

apply from: 'gradle/code-quality.gradle'
apply from: 'gradle/releasing.gradle'
apply from: 'gradle/spotless.gradle'

sourceCompatibility = JavaVersion.VERSION_17

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'org.openjdk.nashorn', name: 'nashorn-core', version: '15.4'
}

jacoco {
    toolVersion = "0.8.8"
}
